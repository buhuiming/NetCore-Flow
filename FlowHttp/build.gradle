plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

//ext.GROUP = "com.lute.network"
//ext.POM_ARTIFACT_ID = "NetCore-Flow"
//ext.VERSION_NAME = "1.7.3"
////引用gradle_upload.gradle
//apply from: "${project.rootDir}/maven_upload.gradle"

afterEvaluate {
    publishing {
        publications {
            // 这个mavenJava可以随便填，只是一个任务名字而已
            // MavenPublication必须有，这个是调用的任务类
            mavenJava(MavenPublication) {
                // 这里头是artifacts的配置信息，不填会采用默认的
                groupId = 'com.github.buhuiming'
                artifactId = 'NetCore-Flow'
                version = '1.7.3'

                from components.release
                if (!project.plugins.hasPlugin('kotlin-android')) {
                    artifact tasks.named("androidSourcesJar").get()
                }
            }
        }
    }
    // 显式声明任务依赖关系
    tasks.matching { it.name.startsWith("generateMetadataFileFor") }.configureEach {
        dependsOn tasks.named("androidSourcesJar")
    }
}
// 用于打包源代码的任务
tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

android {
    compileSdk 35

    defaultConfig {
        minSdk 26
        targetSdk 35
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }
    namespace "com.bhm.network"
}

dependencies {
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.lifecycle:lifecycle-common:2.9.2'
    // Retrofit https://github.com/square/retrofit
    api 'com.squareup.retrofit2:retrofit:2.11.0'
    api 'com.squareup.retrofit2:converter-gson:2.11.0'
    api 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    //https://github.com/square/okhttp
    api 'com.squareup.okhttp3:okhttp:4.12.0'

    implementation  'androidx.appcompat:appcompat:1.7.1'
    implementation  'androidx.fragment:fragment-ktx:1.8.8'
}